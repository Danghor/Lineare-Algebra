////////////////////////////////////////////////////////////////////////
//                                                                    //
// This program computes the determinant of a square matrix A.        //
//                                                                    //
////////////////////////////////////////////////////////////////////////
// This procedure computes the determinant of the matrix a. The matrix a 
// is represented as a list of its rows.
determinant := procedure(A) {
    n := #A;              
    sign := 1;
    for (i in [1 .. n]) {
        r := pivot(A, n, i);
        if (r != i) {
            [ A[i], A[r] ] := [ A[r], A[i] ];   
            sign := -sign;
        }
        if (A[i][i] == 0) {
            return 0;
        }
        for (k in [i+1 .. n]) {
            f := A[k][i]/A[i][i];
            for (j in [i .. n]) {
                A[k][j] -= f * A[i][j];
            }
        }    
    }
    return sign */ [ A[i][i] : i in {1 .. n} ];
};

// Compute the row index j such that the absolute value of
// a[j][i] is maximal among all j in in {i .. n}.
pivot := procedure(A, n, i) {
    r := i;  // index of row containing maximal element
    for (j in [i+1 .. n]) {
        if (abs(A[j][i]) > abs(A[r][i])) {
            r := j;
        }
    }
    return r;
};

////////////////////////////////////////////////////////////////////////
//                                                                    //
// Testing and printing.                                              //
//                                                                    //
////////////////////////////////////////////////////////////////////////

// turns a quadratic matrix into a string
toString := procedure(A) {
    n := #A;
    r := [1 .. n];
    w := max({ #(A[i][j] + "") : i in r, j in r });
    s := "";
    for (i in r) {
        for (j in r) {
            s += pad(A[i][j], w) + " ";
        }    
        s += "\n";
    }
    return s;
};

// left pad number n to a string of length w
pad := procedure(n, w) {
    s := n + "";
    return " " * (w - #s) + s;
};

test := procedure(A) {
    d := determinant(A);
    print("The determinant of:\n\n$toString(A)$\nis $d$.\n");
};

A := [ [1, 2, 3, 4],
       [2, 3, 4, 1],
       [3, 4, 1, 2],
       [4, 1, 2, 3]
     ];

test(A);

A := [ [1, 2, 3],
       [4, 5, 6],
       [7, 8, 9]
     ];

test(A);

A := [ [ 2,  3,  5],
       [ 7, 11, 13],
       [17, 19, 23]
     ];

test(A);
