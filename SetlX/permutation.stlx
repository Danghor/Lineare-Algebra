// The procedure call product(r1, r2) computes the relational
// product r1 o r2.
product := procedure(r1, r2) {
    return { [x,z] : [x,y] in r1, [y,z] in r2 };
};

// Compute the set of all permutations on the set S_n = { 1, ..., n }
allPermutations := procedure(n) {
	return [ toPermutation(l) : l in allLists({1 .. n}) ];
};
// transform a list into a relation
toPermutation := procedure(l) {
	return { [i, l[i]] : i in [1 .. #l] };
};
// For a given set s, this function computes the set of all those lists 
// that contain each of the elements of s exactly once.  In effect, these 
// are all permutations of the set s.
allLists := procedure(s) {
    if (s == {}) {
        return { [] };
    }
    return { [ x ] + l : x in s, l in allLists(s - { x }) };
};

// Given a list of permutations sn, this function associates names with the 
// elements of sn.
makeMap := procedure(sn, nd) {
    print("Printing elements of the group:\n");
    map := {};
    for (i in [ 1 .. #sn ]) {
        name := "p" + fillZeros(i, nd);
        map += { [ sn[i], name ] };
        print("$name$ = $sn[i]$");
    }
    return map;
};

// find non-commuting elements
findNC := procedure(n, map) {
    sn := allPermutations(n);
    for (x in sn) {
        for (y in sn) {
            xy := product(x, y);
            yx := product(y, x);
            if (xy != yx) {
                pxy := map[xy];
                pyx := map[yx];
                px  := map[x];
                py  := map[y];         
                print("non-commuting permutations found:\n");
                print("$px$ = $x$");
                print("$py$ = $y$");
                print("$pxy$ = $xy$");
                print("$pyx$ = $yx$");
                print("");
                print("$px$ * $py$ = $pxy$");
                print("$py$ * $px$ = $pyx$");
                return;
            }
        }
    }
};

// Count the number of digits needed to represent n!, which is n factorial.
countDigits := procedure(n) {
    return ceil(log(n!)/log(10));
};

// Convert the number i to a string that has a given size
fillZeros := procedure(i, numberDigits) {
    s := i + "";
    while (#s < numberDigits) {
        s := "0" + s;
    }
    return s;
};

// Compute the group table and print it.  This will only work if n is less or
// equal than 4, since otherwise the table will be far too big to fit on the
// screen.  For example, the permutation group S5 already has 5! = 120 different
// elements.
groupTable := procedure(n) {
    sn  := allPermutations(n);
    nd  := countDigits(n);
    map := makeMap(sn, nd);
    print("\nPrinting group table:\n");
    s := " " * (1+floor(nd/2)) + "*" + " " * floor((nd + 1)/2) + " |";
    for (i in [ 1 .. #sn ]) {
        s += " p" + fillZeros(i, nd) + " |"; 
    }
    verticalBar := "\n" + "-" * (4 + nd) * (#sn+1) + "\n";  
    s += verticalBar;
    for (px in sn) {
        x := map[px];
        s += " " + x + " |";
        for (py in sn) {
            pz := product(px, py);
            y  := map[py];
            z  := map[pz];
            s += " " + z + " |";        
        }
        s += verticalBar;
    }
    print(s);
    return map;
};

test := procedure(n) {
    map := groupTable(n);
    findNC(n, map);
};

// test(3);
test(4);  // You need a huge screen to see this group!
